plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.12.0"
    id "com.jfrog.bintray" version "1.6"
    id 'nebula.optional-base' version '3.0.3'
}

repositories {
    jcenter()
}

dependencies {
    // used for various utility methods
    compile 'com.google.guava:guava:19.0'

    // used for caching
    compile 'com.github.ben-manes.caffeine:caffeine:2.0.3'

    // needed for jackson package only
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.0.rc1', optional
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.0.rc1', optional
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.0.rc1', optional

    // needed for jooq package only
    compile 'org.jooq:jooq:3.8.2', optional

    // needed for spring.mockmvc package only
    compile 'org.springframework:spring-test:4.3.0.RELEASE', optional
    compile 'org.springframework:spring-web:4.3.0.RELEASE', optional
    compile 'org.springframework:spring-beans:4.3.0.RELEASE', optional
    compile 'javax.servlet:javax.servlet-api:4.0.0-b01', optional

    // needed for spring.gmail package only
    compile 'org.springframework:spring-context-support:4.3.0.RELEASE', optional
    compile 'javax.mail:javax.mail-api:1.5.5', optional

    // unit tests
    testCompile 'junit:junit:4.12'
    // better assertions
    testCompile 'org.assertj:assertj-core:3.4.1'
    // mocking various objects
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    // a little bit of reflection
    testCompile 'org.jooq:joor:0.9.5'
}

group 'eu.goodlike'
version '0.0.1'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = 'UTF-8'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

publishing {
    publications {
        GoodlikeUtils(MavenPublication) {
            groupId 'eu.goodlike'
            artifactId 'goodlike-utils'
            version '0.0.1'

            from components.java
            artifact sourceJar

            pom.withXml {
                asNode().dependencies.dependency.findAll {
                    configurations.compile.allDependencies.find {dep -> dep.name == it.artifactId.text()} &&
                            it.scope.text() == 'runtime'
                }.each {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintray_user')
    key = project.property('bintray_api_key')
    publications = ['GoodlikeUtils']
    pkg {
        repo = 'generic'
        name = 'goodlike-utils'
        userOrg = project.property('bintray_user')
        licenses = ['Unlicence']
        vcsUrl = 'https://github.com/TheGoodlike13/goodlike-utils'
        version {
            name = '0.0.1'
            desc = 'Something new'
            released  = new Date()
            vcsTag = '0.0.1'
            // attributes = []
        }
    }
}
